#compdef toybox
#
# Zsh completion script for Toybox.

__subcommands() {
    local -a commands
    commands=(
        'create'
        'help'
        'list'
        'open'
        'root'
        'version'
    )
    _values Commands $commands
}

__toybox_list() {
  local -a _playgrounds
  _playgrounds=( $(toybox list | sed -E 's/\(.*\)$//g') )
  _describe -t playgrounds "Playgrounds" _playgrounds
}

_toybox() {
    typeset -A opt_args
    local ret=1 state

    _arguments -C \
        '1: :__subcommands' \
        '*:: :->args' \
        && ret=0

    case $state in
        (args)
            case $words[1] in
                (create)
                    _arguments \
                        '--platform: :(iOS macOS tvOS)' \
                        '--force' \
                        '--no-open' \
                        '--input' \
                        && ret=0
                        ;;
                (list)
                    _arguments -C \
                        '--platform: :(iOS macOS tvOS)' \
                        && ret=0
                        ;;
                (open)
                    _arguments \
                        '*: :__toybox_list' \
                        && ret=0
                        ;;
                (help)
                    if (( $CURRENT == 2 ))
                    then
                        __subcommands && ret=0
                    fi
                    ;;
            esac
            ;;
    esac

    return ret
}
